class Player {

    field Square position;
    field TreasureList treasureList;
    
    constructor Player new(Square startPosition) {
        let position = startPosition;
        let treasureList = TreasureList.new(null, null);
        return this;
    }

    /** Deallocates the object's memory. */
    method void dispose() {
        do treasureList.dispose();
        do Memory.deAlloc(this);
        return;
    }
    
    method Square getPosition() {
        return position;
    }
    
    method void setPosition(Square aPosition) {
        let position = aPosition;
        return;
    }
    
    method TreasureList getTreasureList() {
        return treasureList;
    }
    
    method void doMove() {
        var char key;
        var char choice;
        var int roll;
        var Castle castle;
        var boolean validInput;
            
            // write out current player's treasure stash
            do Output.moveCursor(16, 0);
            do Output.printString("Treasures: ");
            do treasureList.writeOutTreasures();
            
            // write out available directions
            do Output.moveCursor(18, 0);
            do position.showPossibleDirections();
            do Output.println();
            
            // write out available actions
            let castle = position.getCastle();
            if (~(castle = null)){
                do Output.printString("Attack Castle: A");
                do Output.println();
            }
            
            // ask player to choose an action until they input something sensible
            let validInput = false;
            while (~(validInput)) {
                do Output.moveCursor(21, 19);
                do Output.printString("Choose an action:");
                let key = Keyboard.readChar();
                
                if (key = 78) {
                    if (~(position.getNorth() = null)) {
                        let position = position.getNorth();
                        let validInput = true;
                    }
                }
                if (key = 69) {
                    if (~(position.getEast() = null)) {
                        let position = position.getEast();
                        let validInput = true;
                    }
                }
                if (key = 83) {
                    if (~(position.getSouth() = null)) {
                        let position = position.getSouth();
                        let validInput = true;
                    }
                }
                if (key = 87) {
                    if (~(position.getWest() = null)) {
                        let position = position.getWest();
                        let validInput = true;
                    }
                }
                if (key = 65) { // 'A'
                    if (~(castle = null)) {
                        // TODO some kind of monster fight to get the treasure (and put all this in another class)
                        if (~(castle.getTreasure() = null)) {
                            do treasureList.add(castle.getTreasure());
                            do castle.setTreasure(null);
                        }
                        let validInput = true;
                    }
                }
                
                if (~(validInput)) {
                    do Output.moveCursor(22, 0);
                    do Output.printString("Please enter a valid option!");
                }
            }
        
        return;
    }
}
